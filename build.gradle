// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.5.0"
    }
}

plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply false
    id 'com.google.gms.google-services' version '4.3.15' apply false
    id 'jacoco' apply true
    id 'org.jlleitschuh.gradle.ktlint' version '11.5.0' apply true
    id "org.sonarqube" version "4.2.1.3168" apply true
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}

tasks.create(name: "unitTestCoverageReport", type: JacocoReport, dependsOn: "testLocalDebugUnitTest") {
    group = "Verification" // existing group containing tasks for generating linting reports etc.
    description = "Generate Jacoco coverage reports for the 'local' debug build."

    reports {
        // human readable (written into './build/reports/jacoco/unitTestCoverageReport/html')
        html {
            enabled true
        }
        // CI-readable (written into './build/reports/jacoco/unitTestCoverageReport/unitTestCoverageReport.xml')
        xml {
            enabled true
        }
    }

    // Execution data generated when running the tests against classes instrumented by the JaCoCo agent.
    // This is enabled with 'enableUnitTestCoverage' in the 'debug' build type.
    executionData.from = "${project.buildDir}/outputs/unit_test_code_coverage/localDebugUnitTest/testLocalDebugUnitTest.exec"

    // Compiled Kotlin class files are written into build-variant-specific subdirectories of 'build/tmp/kotlin-classes'.
    classDirectories.from = "${project.buildDir}/tmp/kotlin-classes/localDebug"

    // To produce an accurate report, the bytecode is mapped back to the original source code.
    sourceDirectories.from = "${project.projectDir}/src/main/java"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses true
    jacoco.excludes = ['jdk.internal.*']
}

task installGitHook(type: Copy) {
    def lintingConfigScript = new File(rootProject.rootDir, '.git/hooks/pre-commit')
    if (!lintingConfigScript.exists()) {
        from new File(rootProject.rootDir, '.githooks/pre-commit')
        into { new File(rootProject.rootDir, '.git/hooks') }
        fileMode 0777
    }
}

tasks.getByPath('app:preBuild').dependsOn installGitHook


sonar {
    properties {
        property "sonar.projectKey", "Sommelier"
        property "sonar.projectName", "Sommelier"
    }
}